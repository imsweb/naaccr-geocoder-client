import org.gradle.internal.jvm.Jvm

plugins {
    id 'java'
    id 'checkstyle'
    id 'findbugs'
    id 'com.bmuschko.nexus' version '2.3.1'
}

group = 'com.imsweb'
version = '1.1'
description = 'Java client library for NAACCR Geocoding Service'

println "Starting build using ${Jvm.current()}"

// UTF-8 for all compilation tasks
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// fail the build if there are compiler warnings
tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:all" << "-Xlint:-processing" << "-Xlint:-serial" << "-Werror"
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.squareup.retrofit2:retrofit:2.2.0'

    testCompile 'junit:junit:4.11'
    testCompile 'com.jcabi:jcabi-matchers:1.3'
}

jar {
    // specify the archive name; otherwise the version is appended to the war file
    archiveName = 'naaccr-geocoder-client.jar'

    manifest {
        attributes('Implementation-Title': 'NAACCR Gecoding Java Client',
                'Implementation-Version': version,
                'Implementation-Vendor': group,
                'Created-By': System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
        )
    }
}

// checkstyle plugin settings
checkstyle {
    ignoreFailures = true
    configFile = file("config/checkstyle/checkstyle.xml")
}

// findbugs plugin settings
findbugs {
    ignoreFailures = true
    effort = 'max'
    excludeFilter = file("config/findbugs/findbugs-exclude.xml")
}

// Gradle wrapper, this allows to build the project without having to install Gradle!
task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

modifyPom {
    project {
        name 'NAACCR Gecodoer Java Client'
        description 'API mapping for NAACCR Geocoder in Java'
        url 'https://github.com/imsweb/naaccr-geocoder-client'
        inceptionYear '2017'

        scm {
            url 'https://github.com/imsweb/naaccr-geocoder-client'
            connection 'scm:https://github.com/imsweb/naaccr-geocoder-client.git'
            developerConnection 'scm:git@github.com:imsweb/naaccr-geocoder-client.git'
        }

        licenses {
            license {
                name 'The MIT License (MIT)'
                url 'http://www.opensource.org/licenses/mit-license.php'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'ctmay4'
                name 'Chuck May'
                email 'mayc@imsweb.com'
            }
        }
    }
}



