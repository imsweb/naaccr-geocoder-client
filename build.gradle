import org.gradle.internal.jvm.Jvm

plugins {
    id 'java-library'
    id 'checkstyle'
    id 'net.ltgt.errorprone' version '0.0.16'
    id 'jacoco'
    id 'com.bmuschko.nexus' version '2.3.1'
    id "io.codearte.nexus-staging" version "0.8.0"
}

group = 'com.imsweb'
version = '1.12'
description = 'Java client library for NAACCR Geocoding Service'
sourceCompatibility = JavaVersion.VERSION_1_8

println "Starting build using ${Jvm.current()}"

// enforce UTF-8, display the compilation warnings
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:all" << "-Xlint:-processing" << "-Xlint:-serial" << "-Werror"
}

repositories {
    mavenCentral()
}

dependencies {
    api 'com.squareup.retrofit2:retrofit:2.3.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'junit-addons:junit-addons:1.4'
    testImplementation 'com.jcabi:jcabi-matchers:1.3'

    errorprone 'com.google.errorprone:error_prone_core:2.3.1'
}

jar {
    // specify the archive name; otherwise the version is appended to the war file
    archiveName = 'naaccr-geocoder-client.jar'

    manifest {
        attributes('Implementation-Title': 'NAACCR Gecoding Java Client',
                'Implementation-Version': version,
                'Implementation-Vendor': group,
                'Created-By': System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Automatic-Module-Name': 'com.imsweb.geocoder')
    }
}

// checkstyle plugin settings
checkstyle {
    configFile = file("config/checkstyle/checkstyle.xml")
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

modifyPom {
    project {
        name 'NAACCR Gecodoer Java Client'
        description 'API mapping for NAACCR Geocoder in Java'
        url 'https://github.com/imsweb/naaccr-geocoder-client'
        inceptionYear '2017'

        scm {
            url 'https://github.com/imsweb/naaccr-geocoder-client'
            connection 'scm:https://github.com/imsweb/naaccr-geocoder-client.git'
            developerConnection 'scm:git@github.com:imsweb/naaccr-geocoder-client.git'
        }

        licenses {
            license {
                name 'The MIT License (MIT)'
                url 'http://www.opensource.org/licenses/mit-license.php'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'ctmay4'
                name 'Chuck May'
                email 'mayc@imsweb.com'
            }
            developer {
                id 'criderp'
                name 'Philip Crider'
                email 'criderp@imsweb.com'
            }
            developer {
                id 'jlorentson'
                name 'Jesse Lorentson'
                email 'jlorentson@imsweb.com'
            }
        }
    }
}

// don't try to release a snapshot to a non-snapshot repository, that won't work anyway
if (version.endsWith('-SNAPSHOT'))
    gradle.startParameter.excludedTaskNames += 'closeAndReleaseRepository'

// Gradle wrapper, this allows to build the project without having to install Gradle...
wrapper {
    gradleVersion = '4.9'
}

