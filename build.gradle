import java.time.Duration

plugins {
    id 'java-library'
    id 'jacoco'
    id 'checkstyle'
    id 'com.github.spotbugs' version '5.2.1'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0' // publish to Maven Central
    id 'com.github.ben-manes.versions' version '0.49.0' // check for out-of-date dependencies (run 'dependencyUpdates' manually)
    id 'org.sonatype.gradle.plugins.scan' version '2.6.1' // scan for vulnerabilities
    id 'org.sonarqube' version '4.4.1.3373' // sonarQube analysis
}

group = 'com.imsweb'
version = '1.18'
description = 'Java client library for NAACCR Geocoding Service'

println "Starting build using JDK ${Runtime.version().feature()}"

repositories {
    mavenCentral()
}

dependencies {
    api 'com.squareup.retrofit2:retrofit:2.9.0'

    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.15.3'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.3'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'junit-addons:junit-addons:1.4'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'com.jcabi:jcabi-matchers:1.7.0'
}

// enforce UTF-8, display the compilation warnings
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:all" << "-Xlint:-processing" << "-Xlint:-serial" << "-Werror"
}

// the Javadoc was made way too strict in Java 8 and it's not worth the time fixing everything!
tasks.withType(Javadoc).configureEach {
    options.addStringOption('Xdoclint:none', '-quiet')
}

// generate javadoc and sources (required by Nexus)
java {
    sourceCompatibility = JavaVersion.VERSION_1_9
    targetCompatibility = JavaVersion.VERSION_1_9

    withJavadocJar()
    withSourcesJar()
}

jar {
    manifest {
        attributes('Implementation-Title': 'NAACCR Gecoding Java Client',
                'Implementation-Version': project.version,
                'Implementation-Vendor': group,
                'Created-By': System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Automatic-Module-Name': 'com.imsweb.geocoder')
    }
}

// checkstyle plugin settings
checkstyle {
    ignoreFailures = false
    configFile = file('config/checkstyle/checkstyle.xml')
    configProperties = ['suppressionFile': file('config/checkstyle/suppressions.xml')]
}


// spotbugs plugin settings
spotbugs {
    ignoreFailures = false
    excludeFilter.set(file('config/spotbugs/spotbugs-exclude.xml'))
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = false
    }
}
test.finalizedBy jacocoTestReport

sonarqube {
    properties {
        property "sonar.projectKey", "imsweb_naaccr-geocoder-client"
        property "sonar.organization", "imsweb"
        property "sonar.host.url", "https://sonarcloud.io"
        property 'sonar.exclusions', '**/lab/*'
        property 'sonar.coverage.exclusions', '**/lab/*'
    }
}

// Nexus vulnerability scan (https://github.com/sonatype-nexus-community/scan-gradle-plugin)
ossIndexAudit {
    outputFormat = 'DEPENDENCY_GRAPH'
    printBanner = false
}

if (project.hasProperty('branch_name') && (project.ext.get('branch_name') == 'master' || project.ext.get('branch_name') == 'main'))
    check.dependsOn 'ossIndexAudit'

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

// https://github.com/ben-manes/gradle-versions-plugin
tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

// needed to deploy to Maven Central
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'naaccr-geocoder-client'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'NAACCR Gecodoer Java Client'
                description = 'API mapping for NAACCR Geocoder in Java'
                url = 'https://github.com/imsweb/naaccr-geocoder-client'
                inceptionYear = '2017'

                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'ctmay4'
                        name = 'Chuck May'
                        email = 'mayc@imsweb.com'
                    }
                    developer {
                        id = 'criderp'
                        name = 'Philip Crider'
                        email = 'criderp@imsweb.com'
                    }
                    developer {
                        id = 'jlorentson'
                        name = 'Jesse Lorentson'
                        email = 'lorentsonj@imsweb.com'
                    }
                    developer {
                        id = 'depryf'
                        name = 'Fabian Depry'
                        email = 'depryf@imsweb.com'
                    }
                }

                scm {
                    url = 'https://github.com/imsweb/naaccr-geocoder-client'
                    connection = 'scm:https://github.com/imsweb/naaccr-geocoder-client.git'
                    developerConnection = 'scm:git@github.com:imsweb/naaccr-geocoder-client.git'
                }
            }
        }
    }
}

// setup JAR signing
signing {
    required { !project.version.endsWith('-SNAPSHOT') }

    String signingKey = project.findProperty('signingKey') ?: ''
    String signingPassword = project.findProperty('signingPassword') ?: ''

    useInMemoryPgpKeys(signingKey, signingPassword)

    sign publishing.publications.mavenJava
}

// needed to release on maven central
nexusPublishing {
    repositories {
        sonatype {
            stagingProfileId = '63e5ddd3ab0d16'

            username = project.findProperty("nexusUsername")
            password = project.findProperty("nexusPassword")
        }
    }

    clientTimeout = Duration.ofSeconds(300)
    connectTimeout = Duration.ofSeconds(60)

    transitionCheckOptions {
        maxRetries.set(50)
        delayBetween.set(Duration.ofMillis(5000))
    }
}

// Gradle wrapper, this allows to build the project without having to install Gradle!
wrapper {
    gradleVersion = '8.4'
    distributionType = Wrapper.DistributionType.ALL
}

